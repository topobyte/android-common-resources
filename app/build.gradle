apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = pGroup
version = pVersion

if (project.hasProperty("topobyte")) {
    apply from: 'file:///' + System.getProperty("user.home") + '/.gradle/topobyte.gradle'
}

android {
    namespace = 'de.topobyte.android.common.resources'

    defaultConfig {
        compileSdk = 35
        minSdkVersion 14
        targetSdkVersion 35
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

project.tasks.withType(GenerateModuleMetadata) {
    enabled = true
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'de.topobyte:android-intent-utils:0.1.0'
    implementation('de.topobyte:bvg-android:0.1.0') {
        exclude group: 'com.google.android'
    }
}

publishing {
    publications {
        release(MavenPublication) {
            afterEvaluate {
                from components.release
            }
        }
    }
}
